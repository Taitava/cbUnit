
//
// PRIVATE DEFINITIONS
// May only be used within this file.
//

Type cbUnit_Application
	Field id
	Field count_tests
	Field count_successes
	Field count_failures
	Field current_context$ // Currently executing test_*() function name.
EndType

Type cbUnit_Failure
	Field id
	Field context$ // Under which test_*() Function this failure occurred.
	Field message$
EndType


//
// PRIVATE FUNCTIONS
// May only be called within this file, or in *.skeleton.cb files
//

Function cbUnit_GetApplication()
	Dim app.cbUnit_Application
	
	app = First(cbUnit_Application)
	If NULL = app Then
		app = New(cbUnit_Application)
		app\id = ConvertToInteger(app)
	EndIf
	Return app\id
EndFunction

Function cbUnit_SetCurrentContext(current_context$)
	Dim app.cbUnit_Application
	
	app = cbUnit_GetApplication()
	app\current_context = current_context
EndFunction

Function cbUnit_Fail(message$)
	Dim app.cbUnit_Application, failure.cbUnit_Failure
	
	app = cbUnit_GetApplication()
	failure = New(cbUnit_failure)
	failure\id = ConvertToInteger(failure)
	failure\context = app\current_context
	failure\message = message
EndFunction

Function cbUnit_FillMessage(message$, a$, b$="", c$="",d$="")
	message = Replace(message, "a$", a)
	message = Replace(message, "b$", b)
	message = Replace(message, "c$", c)
	message = Replace(message, "d$", d)
	Return message
EndFunction

Function cbUnit_HasFailures()
	Dim app.cbUnit_Application
	
	app = cbunit_GetApplication()
	Return app\count_failures > 0
EndFunction

Function cbUnit_EndProgram()
	hook_Cleanup() // Hook For test_*.cb To cleanup Before exit
	cbUnit_WriteResults(CBUNIT_OUTPUT_FILE_PATH, CBUNIT_TEST_FILE_PATH) // These constants are defined in either test_*.cb files, Or If Not present there, cbUnit.cb inserts them To the compilable program.
	End
EndFunction

Function cbUnit_WriteResults(output_file_path$, test_file_path$)
	Dim output_file
	Dim app.cbUnit_Application, failure.cbUnit_Failure
	
	output_file = OpenToEdit(output_file_path$)
	SeekFile output_file, FileSize(output_file_path)
	app = cbUnit_GetApplication()
	WriteLine output_file, ""
	WriteLine output_file, test_file_path+":"
	WriteLine output_file, "  " + app\count_tests + " total assertions."
	WriteLine output_file, "  " + app\count_successes + " succeeded assertions."
	WriteLine output_file, "  " + app\count_failures + " failed assertions."
	If app\count_failures Then
		// Write test failure messages
		WriteLine output_file, "  Failures:"
		For failure.cbUnit_Failure = Each cbUnit_Failure
			WriteLine output_file, "    " + failure\context + "():"
			WriteLine output_file, "      " + failure\message
		Next failure
	EndIf
	CloseFile output_file
EndFunction
