
//
// PUBLIC FUNCTIONS
//

Function Assert(condition, message$="Assertion condition is false.")
	app.cbUnit_Application = cbUnit_GetApplication()
	app\count_tests = app\count_tests + 1
	If condition Then
		// Test succeeded
		app\count_successes = app\count_successes + 1
	Else
		// Test failed
		app\count_failures = app\count_failures + 1
		cbUnit_Fail(message)
		If Upper(Str(CBUNIT_STOP_AT)) = "ASSERT" Then cbUnit_EndProgram()
	EndIf
EndFunction


// Equals
Function AssertEquals(a$, b$, message$ = "a$ should be equal to b$.")
	Assert(a = b, cbUnit_Substitute(message, a,b))
EndFunction

Function AssertNotEquals(a$, b$, message$ = "a$ should not be equal to b$.")
	Assert(a <> b, cbUnit_Substitute(message, a,b))
EndFunction

Function AssertLess(a$, b$, message$ = "a$ should be less than b$.")
	Assert(a < b, cbUnit_Substitute(message, a,b))
EndFunction

Function AssertLessOrEqual(a$, b$, message$ = "a$ should be less than or equal to b$.")
	Assert(a <= b, cbUnit_Substitute(message, a,b))
EndFunction

Function AssertGreater(a$, b$, message$ = "a$ should be greater than b$.")
	Assert(a > b, cbUnit_Substitute(message, a,b))
EndFunction

Function AssertGreaterOrEqual(a$, b$, message$ = "a$ should be greater than or equal to b$.")
	Assert(a >= b, cbUnit_Substitute(message, a,b))
EndFunction


// Strings
Function AssertEmptyString(a$, message$="a$ should be an empty string.")
	Assert(a = "", cbUnit_Substitute(message, a))
EndFunction

Function AssertNotEmptyString(a$, message$="a$ should not be an empty string.")
	Assert(a <> "", cbUnit_Substitute(message, a))
EndFunction

Function AssertInStr(a$, b$, message$="b$ should be present in $a.")
	Assert(InStr(a,b) > 0, cbUnit_Substitute(message, a,b))
EndFunction

Function AssertNotInStr(a$, b$, message$="b$ should not be present in $a.")
	Assert(InStr(a,b) = 0, cbUnit_Substitute(message, a,b))
EndFunction

Function AssertBeginsWith(a$, b$, message="a$ should begin with b$.")
	Assert(Left(a,Len(b)) = b, cbUnit_Substitute(message, a,b))
EndFunction

Function AssertEndsWith(a$, b$, message="a$ should end with b$.")
	Assert(Right(a,Len(b)) = b, cbUnit_Substitute(message, a,b))
EndFunction

Function AssertNotBeginsWith(a$, b$, message="a$ should not begin with b$.")
	Assert(Left(a,Len(b)) <> b, cbUnit_Substitute(message, a,b))
EndFunction

Function AssertNotEndsWith(a$, b$, message="a$ should not end with b$.")
	Assert(Right(a,Len(b)) <> b, cbUnit_Substitute(message, a,b))
EndFunction

Function AssertIsLower(a$, message$="a$ should not contain any uppercase letters.")
	Assert(Lower(a) = a, cbUnit_Substitute(message, a))
EndFunction

Function AssertIsUpper(a$, message$="a$ should not contain any lowercase letters.")
	Assert(Upper(a) = a, cbUnit_Substitute(message, a))
EndFunction


// Math
Function AssertZero(a$, message$="a$ should be zero.") // Use a$ instead of a# so that we can assure that "" is not accidentally interpreted as 0.
	Assert(a="0" Or a="0.0", cbUnit_Substitute(message, a))
EndFunction

// a: The value whose sign should be tested.
// b: The assumed sign, either -1 or 1. Or in some cases 0, but then a is supposed to be zero too, so then you could be using AssertZero(a) instead.
// zero_sign: If a happens to be zero, this determines the sign for it, because it might depend on case, which sign we want to use for zero. Defaults to 0.
Function AssertSign(a#,b, zero_sign=0, message$="The sign of number a$ should be b$, but it is c$.")
	If a = 0 Then
		sign = zero_sign
	Else
		sign = a/Abs(a)
	EndIf
	Assert(sign = b, cbUnit_Substitute(message, a,b,sign))
EndFunction




// Data types
Function AssertInteger(a$, message$="a$ should be an integer.")
	Assert(Str(Int(a)) = a, cbUnit_Substitute(message, a))
EndFunction

Function AssertFloat(a$, message$="a$ should be a float.")
	Assert(Str(Float(a)) = a, cbUnit_Substitute(message, a))
EndFunction

Function AssertIntegerOrFloat(a$, message$="a$ should be an integer or a float.")
	is_integer = Str(Int(a)) = a
	is_float = Str(Float(a)) = a
	Assert(is_integer Or is_float, cbUnit_Substitute(message, a))
EndFunction




//
// PRIVATE DEFINITIONS
//

Type cbUnit_Application
	Field id
	Field count_tests
	Field count_successes
	Field count_failures
	Field current_context$ // Currently executing test_*() function name.
EndType

Type cbUnit_Failure
	Field id
	Field context$ // Under which test_*() Function this failure occurred.
	Field message$
EndType


//
// PRIVATE FUNCTIONS
// May only be called within this file, or in *.skeleton.cb files
//

Function cbUnit_GetApplication()
	app.cbUnit_Application = First(cbUnit_Application)
	If NULL = app Then
		app = New(cbUnit_Application)
		app\id = ConvertToInteger(app)
	EndIf
	Return app\id
EndFunction

Function cbUnit_SetCurrentContext(current_context$)
	app.cbUnit_Application = cbUnit_GetApplication()
	app\current_context = current_context
EndFunction

Function cbUnit_Fail(message$)
	app.cbUnit_Application = cbUnit_GetApplication()
	failure.cbUnit_Failure = New(cbUnit_failure)
	failure\id = ConvertToInteger(failure)
	failure\context = app\current_context
	failure\message = message
EndFunction

Function cbUnit_Substitute(message$, a$, b$="", c$="")
	message = Replace(message, "a$", a)
	message = Replace(message, "b$", b)
	message = Replace(message, "c$", c)
	Return message
EndFunction

Function cbUnit_HasFailures()
	app.cbUnit_Application = cbunit_GetApplication()
	Return app\count_failures > 0
EndFunction

Function cbUnit_EndProgram()
	cbUnit_WriteResults(CBUNIT_OUTPUT_FILE_PATH, CBUNIT_TEST_FILE_PATH)
	End
EndFunction

Function cbUnit_WriteResults(output_file_path$, test_file_path$)
	output_file = OpenToEdit(output_file_path$)
	SeekFile output_file, FileSize(output_file_path)
	app.cbUnit_Application = cbUnit_GetApplication()
	WriteLine output_file, ""
	WriteLine output_file, test_file_path+":"
	WriteLine output_file, "  " + app\count_tests + " total tests."
	WriteLine output_file, "  " + app\count_successes + " succeeded tests."
	WriteLine output_file, "  " + app\count_failures + " failed tests."
	If app\count_failures Then
		// Write test failure messages
		WriteLine output_file, "  Failures:"
		For failure.cbUnit_Failure = Each cbUnit_Failure
			WriteLine output_file, "    " + failure\context + "():"
			WriteLine output_file, "      " + failure\message
		Next failure
	EndIf
	CloseFile output_file
EndFunction
